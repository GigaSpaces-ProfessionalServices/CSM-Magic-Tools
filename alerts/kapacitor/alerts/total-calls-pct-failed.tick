dbrp "telegraf"."autogen"

var snowType = 'Infra'
var snowResource = 'Infra'
var snowMetricName = 'Error rate'

var data = stream
    |from()
        .measurement('nginx_access_log')
    |window()
        .period(5m)
        .every(1m)
        .align()

var goodq = data
    |where(lambda: "resp_code" == '200')
    |count('resp_code')
        .as('count')

var totalq = data
    |count('resp_code')
        .as('count')

totalq
    |join(goodq)
        .as('totalq', 'goodq')
    |default()
        .tag('snowType', snowType)
        .tag('snowResource', snowResource)
        .tag('snowMetricName', snowMetricName)
        .tag('host', 'SystemWide')
    |alert()
        .critReset(lambda: "totalq.count" == 0 OR (float("goodq.count") / float("totalq.count")) > 0.95)
        .crit(lambda: "totalq.count" != 0 AND (float("goodq.count") / float("totalq.count")) < 0.95)
	.stateChangesOnly(1d)
        .id('total-calls-pct-failed')
        .message('Failed queries amount is above 5%')
        .post()
        .endpoint('common-alert')

